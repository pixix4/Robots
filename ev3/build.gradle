group = 'de.westermann.robots.ev3'
version = '0.0.1-alpha'




apply plugin: 'com.bmuschko.docker-remote-api'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.3.1'
    }
}

repositories {
    mavenCentral()
    jcenter()
}


import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile


task cargo {
    doLast {
        file("Cargo.toml").text = """[package]
name = "ev3"
version = "$version"
authors = ["Lars Westermann <lars-westermann@live.de>"]

[[bin]]
name = "ev3"
path = "src/main/rust/main.rs"

[dependencies]
rand = "0.5.1"
time = "0.1.40"
"""
    }
}


task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/docker/Dockerfile')
    from "ev3dev/debian-jessie-cross"

    instruction "RUN sudo apt update"
    instruction "RUN sudo apt --yes install curl"
    instruction "RUN curl https://sh.rustup.rs -sSf | sh -s -- -y"
    instruction "RUN ~/.cargo/bin/rustup target add arm-unknown-linux-gnueabi"
    instruction "RUN  sudo ln -s /usr/bin/arm-linux-gnueabi-gcc /usr/bin/cc"
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile

    inputDir = createDockerfile.destFile.parentFile
    tag = 'pixix4/ev3dev-rust-cross:latest'
}

task createContainer(type: DockerCreateContainer) {
    dependsOn buildImage
    targetImageId { buildImage.getImageId() }

    binds = ["${file('.').absolutePath}": "/ev3"]
    stdinOpen = true
    tty = true
    workingDir = "/ev3"
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId { createContainer.getContainerId() }
}

task removeContainer(type: DockerRemoveContainer) {
    targetContainerId { createContainer.getContainerId() }
}

task stopContainer(type: DockerStopContainer) {
    finalizedBy removeContainer
    targetContainerId { createContainer.getContainerId() }
    onError { exception ->
        if (exception.message != null && exception.message.contains('No such container')) {
            println 'Container not running'
        } else {
            throw exception
        }
    }
}

task runContainer(type: DockerExecContainer) {
    dependsOn cargo
    dependsOn startContainer
    finalizedBy stopContainer

    targetContainerId { createContainer.getContainerId() }
    cmd = ["/home/compiler/.cargo/bin/cargo", "build", "--target", "arm-unknown-linux-gnueabi"]
    successOnExitCodes = [0]

    outputs.file(file("target/arm-unknown-linux-gnueabi/debug/ev3"))
}

task nativeBuild(type: Exec) {
    dependsOn cargo
    executable = "cargo"
    args = ["build"]

    ignoreExitValue = true

    outputs.file(file("target/debug/ev3"))
}

task build() {
    dependsOn runContainer
    //dependsOn nativeBuild

    doLast {
        copy {
            println("Copy robot")
            from runContainer.outputs.files
            into "build"
            rename { it + "-robot" }
        }
        copy {
            println("Copy host")
            from nativeBuild.outputs.files
            into "build"
            rename { it + "-host" }
        }
    }

    outputs.file(file("build/ev3-robot"))
    outputs.file(file("build/ev3-host"))
}


task run(type: Exec) {
    dependsOn cargo
    executable = "cargo"
    args = ["run"]
}


task clean {
    delete("build")
    delete("target")
    delete("Cargo.lock")
    delete("Cargo.toml")
}

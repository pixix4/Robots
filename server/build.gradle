import java.text.SimpleDateFormat

group = 'de.westermann.robots.server'
version = '0.0.1-alpha'

apply plugin: "kotlin"
apply plugin: "application"

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

mainClassName = 'de.westermann.robots.server.Main'

static def buildTime() {
    def format = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss z")
    format.setTimeZone(TimeZone.getTimeZone("UTC"))
    return format.format(new Date())
}

jar {
    includeEmptyDirs = false

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }.findAll {
            !it.displayName.contains("kotlin-stdlib-js")
        }
    }

    manifest {
        manifest.attributes "Main-Class": mainClassName

        manifest.attributes "Build-Time": buildTime()
        manifest.attributes "Build-Version": version
        manifest.attributes "Build-Tools": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}, java-${System.getProperty('java.version')}"
        manifest.attributes "Build-System": "${System.getProperty("os.name")} '${System.getProperty("os.version")}' (${System.getProperty("os.arch")})"
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', "about.html"
}

run {
    standardInput = System.in
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    compile "io.javalin:javalin:1.6.0"
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.fusesource.jansi:jansi:1.8" // Needed for windows logging colors

    compile "com.lambdaworks:scrypt:1.4.0"
    compile "commons-io:commons-io:2.6"
    compile "com.vaadin:vaadin-sass-compiler:0.9.13"

    compile 'io.moquette:moquette-broker:0.10'

    compile "com.github.WilliamAHartman:Jamepad:1.1"

    compile project(":robot")
    compile project(":website")

    //compile fileTree(dir: 'libs', include: ['*.jar'])
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

